//////////////////////////////////////////////////////////////////////////////
// Base event file
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
// #TOTAL_EVENTS is a required entry, and
// should be # of events we have triggers for
// and code fore
// KCK WARNING: KEEP THIS UPDATED!
//////////////////////////////////////////////////////////////////////////////
#TOTAL_EVENTS 22

//////////////////////////////////////////////////////////////////////////////
// #SET_EVENT will set the activated flag for any
// events we want to be initially activate
// ie: Seoul already should be marked as
// captured in the Pusan parameter scenario
//
// This marks the ROK as initially on the defensive
//////////////////////////////////////////////////////////////////////////////
#SET_EVENT 10

//////////////////////////////////////////////////////////////////////////////
// This sets the initial tempo
//////////////////////////////////////////////////////////////////////////////
#SET_TEMPO 150

//////////////////////////////////////////////////////////////////////////////
// Initialization section done
//////////////////////////////////////////////////////////////////////////////
#ENDINIT


//////////////////////////////////////////////////////////////////////////////
// Event #1
// Seoul being attacked by DPRK forces
//////////////////////////////////////////////////////////////////////////////
#IF_NOT_EVENT_PLAYED 1
	#IF_CONTROLLED 6 OR 1244 423 242 304 368 230 225 1934
		#PLAY_MOVIE 100
		#SET_PAK_PRIORITY 6 228 100
		#SET_PAK_PRIORITY 2 228 100
		#CHANGE_PRIORITIES 6 2
		#CHANGE_PRIORITIES 2 1
		#SET_TEMPO 255
		#DO_EVENT 1
		#DO_MONOLOG Seoul being attacked by DPRK forces
	#ENDIF
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Event #2
// Seoul falls/regained
//////////////////////////////////////////////////////////////////////////////
#IF_EVENT_PLAYED 2
	#IF_CONTROLLED 2 AND 226 227 228 295
		#SHIFT_INITIATIVE 2 6 50
		#RESET_EVENT 2
		#DO_MONOLOG Seoul regained
	#ENDIF
#ELSE
	#IF_CONTROLLED 6 AND 226 227 228 295
		#PLAY_MOVIE 101
		#DO_EVENT 2
		#SHIFT_INITIATIVE 6 2 50
		#DO_MONOLOG Seoul falls
	#ENDIF
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Event #3
// P'yongyang being attacked by ROK forces
//////////////////////////////////////////////////////////////////////////////
#IF_NOT_EVENT_PLAYED 3
	#IF_CONTROLLED 2 OR 259 1071 1953 675 45 1656 1949 3141
		#PLAY_MOVIE 102
		#SET_PAK_PRIORITY 6 260 100
		#SET_PAK_PRIORITY 2 260 100
		#CHANGE_PRIORITIES 6 1
		#CHANGE_PRIORITIES 2 2
		#SET_TEMPO 255
		#DO_EVENT 3
		#DO_MONOLOG P'yongyang being attacked by ROK forces
	#ENDIF
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Event #4
// P'yongyang falls/regained
//////////////////////////////////////////////////////////////////////////////
#IF_EVENT_PLAYED 4
	#IF_CONTROLLED 6 AND 680 260 
		#SHIFT_INITIATIVE 2 6 50
		#RESET_EVENT 4
		#DO_MONOLOG P'yongyang regained
	#ENDIF
#ELSE
	#IF_CONTROLLED 2 AND 680 260
		#PLAY_MOVIE 103
		#SET_PAK_PRIORITY 6 399 100
		#SET_PAK_PRIORITY 2 399 100
		#DO_EVENT 4
		#SHIFT_INITIATIVE 6 2 50
		#DO_MONOLOG P'yongyang falls
	#ENDIF
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Event #5
// DPRK advanced beyond DMZ
//////////////////////////////////////////////////////////////////////////////
#IF_NOT_EVENT_PLAYED 5
	#IF_NOT_EVENT_PLAYED 7
		#IF_CONTROLLED 6 OR 126 1836 1833 1826 339 340 232 794
			#DO_EVENT 5
			#RESET_EVENT 6
			#DO_MONOLOG DPRK advanced beyond DMZ
		#ENDIF
	#ENDIF
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Event #6
// DPRK pushed back to DMZ
//////////////////////////////////////////////////////////////////////////////
#IF_EVENT_PLAYED 5
	#IF_EVENT_PLAYED 7
		#PLAY_MOVIE 104
		#DO_EVENT 6
		#RESET_EVENT 5
		#DO_MONOLOG DPRK pushed back to DMZ
	#ELSE
		#IF_CONTROLLED 2 AND 759 2241 12 345 1799 1829 936 3177 1239 412
			#PLAY_MOVIE 104
			#DO_EVENT 6
			#RESET_EVENT 5
			#DO_MONOLOG DPRK pushed back to DMZ
		#ENDIF
	#ENDIF
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Event #7
// Combined forces advanced beyond DMZ
//////////////////////////////////////////////////////////////////////////////
#IF_NOT_EVENT_PLAYED 7
	#IF_NOT_EVENT_PLAYED 5
		#IF_CONTROLLED 2 OR 1204 1206 1832 1831 1798 14 1030 1993
			#DO_EVENT 7
			#RESET_EVENT 8
			#DO_MONOLOG Combined forces advanced beyond DMZ
		#ENDIF
	#ENDIF
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Event #8
// Combined forces pushed back to DMZ
//////////////////////////////////////////////////////////////////////////////
#IF_EVENT_PLAYED 7
	#IF_EVENT_PLAYED 5
		#PLAY_MOVIE 105
		#DO_EVENT 8
		#RESET_EVENT 7
		#DO_MONOLOG Combined forces pushed back to DMZ
	#ELSE
		#IF_CONTROLLED 6 AND 410 2694 3171 2695 1950 108 344 2242 266
			#PLAY_MOVIE 105
			#DO_EVENT 8
			#RESET_EVENT 7
			#DO_MONOLOG Combined forces pushed back to DMZ
		#ENDIF
	#ENDIF
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Event #9
// Combined forces go on offensive
//////////////////////////////////////////////////////////////////////////////
#IF_EVENT_PLAYED 10
	#IF_ON_OFFENSIVE 2
		#PLAY_MOVIE 106
		#DO_EVENT 9
		#RESET_EVENT 10
		#DO_MONOLOG Combined forces go on offensive
	#ENDIF
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Event #10
// Combined forces go on defensive
//////////////////////////////////////////////////////////////////////////////
#IF_EVENT_PLAYED 9
	#IF_ON_OFFENSIVE 6
		#PLAY_MOVIE 107
		#DO_EVENT 10
		#RESET_EVENT 9
		#DO_MONOLOG Combined forces go on defensive
	#ENDIF
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Event #11
// China joins the war
// By US/ROK controlling any of the following
// Souther Sector CCC, Sariwon, Soktam-ni
// or it being day 3
//////////////////////////////////////////////////////////////////////////////
#IF_NOT_EVENT_PLAYED 11
	#IF_CAMPAIGN_DAY >= 2
		#PLAY_MOVIE 108
		#CHANGE_RELATIONS 5 6 1
		#DO_EVENT 11
		#DO_MONOLOG China does join DPRK
	#ENDIF
#ELSE
	#IF_CONTROLLED 2 OR 253 741 1804
		#PLAY_MOVIE 108
		#CHANGE_RELATIONS 5 6 1
		#DO_EVENT 11
		#DO_MONOLOG China does join DPRK
	#ENDIF
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Event #12
// Russia joins the war
//////////////////////////////////////////////////////////////////////////////
#IF_NOT_EVENT_PLAYED 12
	#IF_CONTROLLED 2 AND 404
		#PLAY_MOVIE 109
		#CHANGE_RELATIONS 4 6 1
		#DO_EVENT 12
		#DO_MONOLOG Russia does join DPRK
	#ENDIF
#ELSE
	#IF_CAMPAIGN_DAY >= 5
		#PLAY_MOVIE 109
		#CHANGE_RELATIONS 4 6 1
		#DO_EVENT 12
		#DO_MONOLOG Russia does join DPRK
	#ENDIF
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Event #13
// Bad guys win
// Basically, did Seoul and Pusan fall
//////////////////////////////////////////////////////////////////////////////
#IF_CONTROLLED 6 AND 228 417
	#PLAY_MOVIE 113
	#DO_EVENT 13
	#END_GAME 13
	#DO_MONOLOG Basically, did Seoul and Pusan fall -> END GAME
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Event #14
// Stalemate
//////////////////////////////////////////////////////////////////////////////
#IF_BORDOM_HOURS 480
	#PLAY_MOVIE 114
	#DO_EVENT 14
	#END_GAME 14
	#DO_MONOLOG Stalemate -> END GAME
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Event #15
// Timeout
//////////////////////////////////////////////////////////////////////////////
#IF_CAMPAIGN_DAY >= 30
	#PLAY_MOVIE 115
	#DO_EVENT 15
	#END_GAME 15
	#DO_MONOLOG Timeout -> END GAME
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Event #16
// We win - hu-rah!
// Basically - We own Wonsan and P'yongyang
//////////////////////////////////////////////////////////////////////////////
#IF_CONTROLLED 2 AND 680 404
	#PLAY_MOVIE 116
	#DO_EVENT 16
	#END_GAME 16
	#DO_MONOLOG Basically - We own Wonsan and P'yongyang -> END GAME
#ENDIF

//////////////////////////////////////////////////////////////////////////////
// Script done
//////////////////////////////////////////////////////////////////////////////
#ENDSCRIPT